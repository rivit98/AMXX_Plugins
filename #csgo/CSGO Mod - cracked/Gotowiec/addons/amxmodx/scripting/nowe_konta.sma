/* Script generated by Pawn Studio */

#include <amxmodx>
#include <amxmisc>
#include <fakemeta>
#include <hamsandwich>
//#include <csgo>
#include <nvault>
#include <colorchat>

new g_iMsgScreenFade;
new bool:g_bHasloWpisane[33];

/*
#define czy_w_trakcie_logowania(%1) (task_exists(%i+333))
*/
public plugin_init()
{
	register_plugin("Nick na haslo", "1.0", "d0naciak.pl");
	
	register_clcmd("joinclass", "cmd_WybralStroj");
	register_clcmd("jointeam 6", "cmd_WybralSpecta");
	register_menucmd(register_menuid("#Team_Select_Spect"), 51, "menu_WybralDruzyne")
	register_menucmd(register_menuid("#Terrorist_Select"), MENU_KEY_1|MENU_KEY_2|MENU_KEY_3|MENU_KEY_4|MENU_KEY_5, "menu_WybralStroj")
	register_menucmd(register_menuid("#CT_Select"), MENU_KEY_1|MENU_KEY_2|MENU_KEY_3|MENU_KEY_4|MENU_KEY_5, "menu_WybralStroj")
	
	register_clcmd("say", "cmd_Blokada");
	register_clcmd("say_team", "cmd_Blokada");
	RegisterHam(Ham_Spawn, "player", "fw_Odrodzenie_Post", 1);
	//register_event("ScreenFade", "ev_ScreenFade", "b", "7<254", "7>254");
	
	register_clcmd("say /konto", "cmd_Konto");
	register_clcmd("WpiszSwojeHaslo", "cmd_HasloWpisane");
	register_clcmd("UstawHaslo", "cmd_NoweHasloWpisane");
	
	g_iMsgScreenFade = get_user_msgid("ScreenFade");
	register_message(g_iMsgScreenFade, "msg_ScreenFade");
}

public plugin_natives() {
	register_native("is_user_logged", "nat_CzyGraczJestZalogowany", 1);
}

public nat_CzyGraczJestZalogowany(id) {
	return _:g_bHasloWpisane[id];
}

public client_connect(id) {
	g_bHasloWpisane[id] = false;
}

public client_putinserver(id) {
	if(!is_user_hltv(id)) {
		set_task(30.0, "task_KickAfk", id);
	}
}

public client_disconnect(id)
{
	remove_task(id);
	remove_task(id + 333);
}

public cmd_Konto(id)
{
	new iMenu = menu_create("System Rezerwacji Nicku^nby \rd0naciak.pl", "Konto_Handler");
	
	menu_additem(iMenu, "Utworz haslo");
	menu_additem(iMenu, "Usun haslo");
	menu_additem(iMenu, "Informacje o haslach");
	
	menu_display(id, iMenu);
	
	return PLUGIN_HANDLED;
}

public Konto_Handler(id, iMenu, iItem)
{
	switch(iItem)
	{
		case 0: UtworzHaslo(id);
		case 1: UsunHaslo(id);
		case 2: InfoHasla(id);
	}
	
	menu_destroy(iMenu);
}

public UtworzHaslo(id)
{
	new iVault = nvault_open("Konta_donaciak");
	new szNick[32], szHaslo[2];
	
	get_user_name(id, szNick, 31);
	
	if(nvault_get(iVault, szNick, szHaslo, 1))
	{
		ColorChat(id, GREEN, "[KONTO]^x01 Posiadasz juz haslo na swoim nicku!");
		nvault_close(iVault);
		return PLUGIN_HANDLED;
	}
	
	client_cmd(id, "messagemode UstawHaslo");
	
	ColorChat(id, GREEN, "[KONTO]^x01 Wpisz teraz haslo, jakie chcesz ustawic.");
	ColorChat(id, GREEN, "[KONTO]^x01 Haslo moze posiadac maksymalnie^x03 32 znaki!");
	ColorChat(id, GREEN, "[KONTO]^x01 Uwaznie sprawdzaj co wpisujesz!");
	
	nvault_close(iVault);
	return PLUGIN_HANDLED;
}

public UsunHaslo(id)
{
	new iMenu = menu_create("Jestes pewny ze chcesz usunac haslo?", "UsunHaslo_Handler");
	
	menu_additem(iMenu, "Tak");
	menu_additem(iMenu, "Nie");
	
	menu_setprop(iMenu, MPROP_EXIT, MEXIT_NEVER);
	
	menu_display(id, iMenu);
}

public UsunHaslo_Handler(id, iMenu, iItem)
{
	switch(iItem)
	{
		case 0:
		{
			new szNick[32], iVault = nvault_open("Konta_donaciak");
			get_user_name(id, szNick, 31);
			nvault_remove(iVault ,szNick);
			nvault_close(iVault);
			
			ColorChat(id, GREEN, "[KONTO]^x01 Haslo zostalo usuniete!");
		}
		case 1: ColorChat(id, GREEN, "[KONTO]^x01 Usuwanie hasla zostalo anulowane.");
	}
	
	menu_destroy(iMenu);
}

public InfoHasla(id)
	show_menu(id, 1023, "\wDzieki zalozeniu haslo na konto, ^n \
								mozesz byc pewny, ze nikt nie wlamie sie na twoje konto! ^n \
								Jezeli masz ustawione haslo na nick, co kazde wejscie na ^n \
								serwer, zostaniesz zapytany o haslo na nick, podanie ^n \
								blednego lub niepodanie hasla wcale, wyrzuci Cie z serwera.");
			
	


public cmd_WybralStroj(id) {
	WybralDruzyne(id);
}

public cmd_WybralSpecta(id) {
	WybralDruzyne(id);
}

public menu_WybralDruzyne(id, iItem) {
	if(iItem == 5) {
		WybralDruzyne(id);
	}
}

public menu_WybralStroj(id, iItem) {
	WybralDruzyne(id);
}

public WybralDruzyne(id) {
	if(!is_user_connected(id) || g_bHasloWpisane[id]) {
		return PLUGIN_CONTINUE;
	}
	
	remove_task(id);
	CzyMaHaslo(id);
	
	return PLUGIN_CONTINUE;
	
}

public CzyMaHaslo(id)
{
	new iVault = nvault_open("Konta_donaciak");
	new szNick[32]; get_user_name(id, szNick, 31);
	new szHaslo[2];
	
	if(!nvault_get(iVault, szNick, szHaslo, 1)) {
		g_bHasloWpisane[id] = true;
		nvault_close(iVault);
		
		ColorChat(id, GREEN, "[KONTO]^x01 Boisz sie ze ktos^x03 wlamie sie na twoj nick!?");
		ColorChat(id, GREEN, "[KONTO]^x01 Uzyj komendy^x03 /konto^x01 aby zarezerwowac na swoj nick haslo!");
		return;
	}
	
	nvault_close(iVault);
	
	Display_Fade(id, 1, 1, 0x0004, 0, 0, 0, 254);
	set_pev(id, pev_flags, pev(id, pev_flags) | FL_FROZEN);
	
	set_task(2.0, "PytajHaslo", id + 333, _, _, "b");
	set_task(60.0, "task_Kick", id);
	
	ColorChat(id, GREEN, "[KONTO]^x01 Na koncie jest zalozone haslo...Poczekaj chwilke.");
}

public PytajHaslo(id)
{
	new iMenu = menu_create("Na twoj nick ustawione jest haslo, co dalej?", "PytajHaslo_Handler");
	
	menu_additem(iMenu, "Wpisz haslo");
	menu_additem(iMenu, "Nie znam hasla");
	
	menu_setprop(iMenu, MPROP_EXIT, MEXIT_NEVER);
	menu_display(id - 333, iMenu);
	
	return PLUGIN_CONTINUE;
}

public PytajHaslo_Handler(id, iMenu, iItem)
{
	switch(iItem)
	{
		case 0:
		{
			client_cmd(id, "messagemode WpiszSwojeHaslo");
			
			ColorChat(id, GREEN, "[KONTO]^x01 Wpisz teraz swoje haslo.");
			ColorChat(id, GREEN, "[KONTO]^x01 Wpisz teraz swoje haslo.");
			ColorChat(id, GREEN, "[KONTO]^x01 Wpisz teraz swoje haslo.");
		}
		case 1: server_cmd("kick #%d ^"Nie wpisales hasla!^"", get_user_userid(id));
	}
	
	remove_task(id + 333);
	menu_destroy(iMenu);
}

public cmd_Blokada(id)
{
	if(!g_bHasloWpisane[id]) 
		return PLUGIN_HANDLED;
	
	return PLUGIN_CONTINUE;
}

public fw_Odrodzenie_Post(id) {
	if(task_exists(id)) {
		set_pev(id, pev_flags, pev(id, pev_flags) | FL_FROZEN);
	}
}

public msg_ScreenFade(iMsgId, iMsgDest, id) {
	if(task_exists(id)) {
		return PLUGIN_HANDLED;
	}
	
	return PLUGIN_CONTINUE;
}

public cmd_HasloWpisane(id)
{
	new iVault = nvault_open("Konta_donaciak");
	new szHasloWpisane[32], szHaslo[32], szNick[32];
	
	get_user_name(id, szNick, 31);
	
	read_argv(1, szHasloWpisane, 31);
	nvault_get(iVault, szNick, szHaslo, 31);
	nvault_close(iVault);
	
	if(!equal(szHaslo, szHasloWpisane))
	{
		client_cmd(id, "messagemode WpiszSwojeHaslo");
		ColorChat(id, GREEN, "[KONTO]^x01 Wpisane haslo jest nieprawidlowe! Sprobuj ponownie...");
		return PLUGIN_HANDLED;
	}
	
	remove_task(id);
	
	Display_Fade(id, 1, 0, 0x0000, 0, 0, 0, 255);
	set_pev(id, pev_flags, pev(id, pev_flags) & ~FL_FROZEN);
	g_bHasloWpisane[id] = true;
	
	ColorChat(id, GREEN, "[KONTO]^x01 Wpisano poprawne haslo :)");
	
	return PLUGIN_HANDLED;
}

public cmd_NoweHasloWpisane(id)
{
	new szHaslo[32], szNick[32], iVault = nvault_open("Konta_donaciak");
	
	get_user_name(id, szNick, 31);
	read_argv(1, szHaslo, 31);
	nvault_set(iVault ,szNick, szHaslo);
	nvault_close(iVault);
	
	ColorChat(id, GREEN, "[KONTO]^x01 Ustawiles haslo:^x03 %s", szHaslo);
	ColorChat(id, GREEN, "[KONTO]^x01 Ustawiles haslo:^x03 %s", szHaslo);
	ColorChat(id, GREEN, "[KONTO]^x01 Ustawiles haslo:^x03 %s", szHaslo);
	ColorChat(id, GREEN, "[KONTO]^x01 Pomyliles sie? Usun haslo i ustaw na nowo.");
}

public task_Kick(id)
	server_cmd("kick #%d ^"Nie wpisales hasla na czas!^"", get_user_userid(id));
	
public task_KickAfk(id)
	server_cmd("kick #%d ^"AFK - nie byles aktywny przez 30 sec.^"", get_user_userid(id));
	
stock Display_Fade(id,duration,holdtime,fadetype,red,green,blue,alpha)
{
    message_begin( !id ? MSG_ALL : MSG_ONE, g_iMsgScreenFade,{0,0,0},id );
    write_short( (1<<12) * duration );  // Duration of fadeout
    write_short( (1<<12) * holdtime );  // Hold time of color
    write_short( fadetype );    // Fade type
    write_byte ( red );         // Red
    write_byte ( green );       // Green
    write_byte ( blue );        // Blue
    write_byte ( alpha );       // Alpha
    message_end();
}
