/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <amxmisc>
#include <csgo>

new g_iOpcjaGracza[33], g_iCelGracza[33];
new g_iZawartoscDoDodania[33][2];

public plugin_init() {
	register_plugin("CSGO Mod: Komendy admina", "1.0", "donaciak.pl");
	
	register_clcmd("say /csgoadmin", "cmd_MenuAdmina");
	register_clcmd("csgoadmin_Ilosc", "cmd_WpisalIlosc");
	
	register_concmd("csgo_addskins", "cmd_DodajSkiny", ADMIN_IMMUNITY, "<nick> <CSWID broni> <nazwa_skina> <ilosc>");
	register_concmd("csgo_addcrates", "cmd_DodajSkrzynki", ADMIN_IMMUNITY, "<nick> <nazwa_skrzynki> <ilosc>");
	register_concmd("csgo_addkeys", "cmd_DodajKlucze", ADMIN_IMMUNITY, "<nick> <ilosc>");
	register_concmd("csgo_addeuro", "cmd_DodajEuro", ADMIN_IMMUNITY, "<nick> <ilosc>");
	
}

public cmd_WpisalIlosc(id) {
	if(!(get_user_flags(id) & ADMIN_IMMUNITY)) {
		return PLUGIN_HANDLED;
	}
	
	new iTarget = g_iCelGracza[id];
	if(!iTarget) {
		csgo_print_message(id, "Nie znaleziono gracza!");
		return PLUGIN_HANDLED;
	}
	
	
	new szIlosc[32], szNick[32], iIlosc;
	read_argv(1, szIlosc, 31);
	replace(szIlosc, 31, ".", "");
	iIlosc = str_to_num(szIlosc);
	get_user_name(iTarget, szNick, 31);
	
	switch(g_iOpcjaGracza[id]) {
		case 1: {
			new iIdBroni = g_iZawartoscDoDodania[id][0], iSkin = g_iZawartoscDoDodania[id][1];
			new szNazwaBroni[16], szNazwaSkina[32];
			
			csgo_get_short_weaponname(iIdBroni, szNazwaBroni, 15);
			csgo_get_skin_name(iIdBroni, iSkin, szNazwaSkina, 31);
			
			csgo_set_user_skins(iTarget, iIdBroni, iSkin, csgo_get_user_skins(iTarget, iIdBroni, iSkin) + iIlosc);
			csgo_print_message(id, "Graczowi^x03 %s^x01 dodano^x03 %dx Skin %s - %s", szNick, iIlosc, szNazwaBroni, szNazwaSkina);
		}
		
		case 2: {
			new iSkrzynka = g_iZawartoscDoDodania[id][0];
			new szNazwaSkrzynki[32];
			
			csgo_get_crate_name(iSkrzynka, szNazwaSkrzynki, 31);
			
			csgo_set_user_crates(iTarget, iSkrzynka, csgo_get_user_crates(iTarget, iSkrzynka) + iIlosc);
			csgo_print_message(id, "Graczowi^x03 %s^x01 dodano^x03 %dx %s", szNick, iIlosc, szNazwaSkrzynki);
		}
		
		case 3: {
			csgo_set_user_keys(iTarget, csgo_get_user_keys(iTarget) + iIlosc);
			csgo_print_message(id, "Graczowi^x03 %s^x01 dodano^x03 %dx klucze do skrzynki", szNick, iIlosc);
		}
		
		case 4: {
			csgo_set_user_euro(iTarget, csgo_get_user_euro(iTarget) + iIlosc);
			csgo_format_euro(iIlosc, szIlosc, 31);
			csgo_print_message(id, "Graczowi^x03 %s^x01 dodano^x03 %s Euro", szNick, szIlosc);
		}
	}
	
	return PLUGIN_HANDLED;
}
			
			
public cmd_MenuAdmina(id) {
	if(!(get_user_flags(id) & ADMIN_IMMUNITY)) {
		return PLUGIN_CONTINUE;
	}
	
	new iMenu = menu_create("Menu admina CS:GO Mod", "MenuAdmina_Handler");
	
	menu_additem(iMenu, "Dodaj skiny");
	menu_additem(iMenu, "Dodaj skrzynki");
	menu_additem(iMenu, "Dodaj klucze");
	menu_additem(iMenu, "Dodaj euro");
	
	menu_display(id, iMenu);
	return PLUGIN_HANDLED;
}

public MenuAdmina_Handler(id, iMenu, iItem) {
	if(iItem == MENU_EXIT) {
		menu_destroy(iMenu);
		return PLUGIN_CONTINUE;
	}
	
	g_iOpcjaGracza[id] = iItem + 1;
	WybierzGracza(id);
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public WybierzGracza(id) {
	static iIloscGraczy;
	new iMenu = menu_create("Wybierz gracza: ", "WybierzGracza_Handler");
	new szId[4], szNick[32];
	
	if(!iIloscGraczy) {
		iIloscGraczy = get_maxplayers();
	}
	
	for(new i = 1; i <= iIloscGraczy; i++) {
		if(is_user_connected(i) && !is_user_hltv(i)) {
			num_to_str(i, szId, 3);
			get_user_name(i, szNick, 31);
			
			menu_additem(iMenu, szNick, szId);
		}
	}
	
	menu_display(id, iMenu);
}

public WybierzGracza_Handler(id, iMenu, iItem) {
	if(iItem < 0) {
		if(iItem == MENU_EXIT) {
			menu_destroy(iMenu);
		}
		
		return PLUGIN_CONTINUE;
	}
	
	new iAccess, iCb, szId[4], szNick[32];
	menu_item_getinfo(iMenu, iItem, iAccess, szId, 3, szNick, 31, iCb);
	
	g_iCelGracza[id] = str_to_num(szId);
	csgo_print_message(id, "Wybrano^x03 %s", szNick);
	
	switch(g_iOpcjaGracza[id]) {
		case 1: WybierzBron(id);
		case 2: WybierzSkrzynke(id);
		case 3, 4: PodajIlosc(id);
	}
	
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}


public WybierzBron(id) {
	new iMenu = menu_create("Wybierz bron:", "WybierzBron_Handler");
	new szNazwaBroni[16], szCswId[4];
	
	for(new i = 1; i < 31; i++)
	{
		if(CSGO_BLOCKWPNSKINS & (1<<i) || !csgo_get_skinsnum(i))
			continue;
			
		csgo_get_short_weaponname(i, szNazwaBroni, 15);
		num_to_str(i, szCswId, 3);
		
		menu_additem(iMenu, szNazwaBroni, szCswId);
	}
	
	if(!szCswId[0]) {
		csgo_print_message(id, "Nie znaleziono zadnych skinow!");
		menu_destroy(iMenu);
	}
	else {
		menu_display(id, iMenu);
	}
}

public WybierzBron_Handler(id, iMenu, iItem) {
	if(iItem < 0) {
		if(iItem == MENU_EXIT)
			menu_destroy(iMenu);
		
		return PLUGIN_CONTINUE;
	}
	
	new iAccess, iCb, szCswId[4], szNazwaBroni[32];
	menu_item_getinfo(iMenu, iItem, iAccess, szCswId, 3, szNazwaBroni, 31, iCb);
	
	csgo_print_message(id, "Wybrano^x03 %s", szNazwaBroni);
	new iNoweMenu = menu_create("Wybierz skina, ktorego chcesz dodac:", "WybierzSkina_Handler");
	new iCswId = g_iZawartoscDoDodania[id][0] = str_to_num(szCswId);
	new szSkin[32], szSkinId[4];
	
	for(new i = 1; i <= csgo_get_skinsnum(iCswId); i++) {
		num_to_str(i, szSkinId, 3);
		csgo_get_skin_name(iCswId, i, szSkin, 31);
		
		menu_additem(iNoweMenu, szSkin, szSkinId);
	}
	
	menu_display(id, iNoweMenu);
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public WybierzSkina_Handler(id, iMenu, iItem) {
	if(iItem < 0) {
		if(iItem == MENU_EXIT)
			menu_destroy(iMenu);
		
		return PLUGIN_CONTINUE;
	}
	
	new iAccess, iCb, szSkinId[4], szNazwaSkina[32];
	menu_item_getinfo(iMenu, iItem, iAccess, szSkinId, 3, szNazwaSkina, 31, iCb);
	
	csgo_print_message(id, "Wybrano^x03 %s", szNazwaSkina);
	g_iZawartoscDoDodania[id][1] = str_to_num(szSkinId);
	PodajIlosc(id);
		
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public WybierzSkrzynke(id) {
	new iMenu = menu_create("Wybierz skrzynke:", "WybierzSkrzynke_Handler");
	new szNazwa[32], szSkrzynkaId[4];
	
	for(new i = 1; i <= csgo_get_cratesnum(); i++)
	{
		csgo_get_crate_name(i, szNazwa, 31);
		num_to_str(i, szSkrzynkaId, 3);
		
		menu_additem(iMenu, szNazwa, szSkrzynkaId);
	}
	
	if(!szSkrzynkaId[0]) {
		csgo_print_message(id, "Nie znaleziono zadnych skrzynek!");
	}
	else {
		menu_display(id, iMenu);
	}
}

public WybierzSkrzynke_Handler(id, iMenu, iItem) {
	if(iItem < 0) {
		if(iItem == MENU_EXIT)
			menu_destroy(iMenu);
		
		return PLUGIN_CONTINUE;
	}
	
	new iAccess, iCb, szSkrzynkaId[4], szNazwaSkrzynki[32];
	menu_item_getinfo(iMenu, iItem, iAccess, szSkrzynkaId, 3, szNazwaSkrzynki, 31, iCb);
	csgo_print_message(id, "Wybrano^x03 %s", szNazwaSkrzynki);
	
	g_iZawartoscDoDodania[id][0] = str_to_num(szSkrzynkaId);
	PodajIlosc(id);
	
	menu_destroy(iMenu);
	return PLUGIN_CONTINUE;
}

public PodajIlosc(id) {
	client_cmd(id, "messagemode csgoadmin_Ilosc");
	csgo_print_message(id, "Wpisz ilosc, jaka ma zostac dodane.");
}
	
public cmd_DodajSkrzynki(id, iLevel, iCId)
{
	if(!cmd_access(id, iLevel, iCId, 4))
		return PLUGIN_HANDLED;
	
	new szNick[32], szNazwaSkrzynki[32], szIlosc[32], iIlosc, iTarget, iSkrzynka;
	
	read_argv(1, szNick, 31);
	read_argv(2, szNazwaSkrzynki, 31);
	read_argv(3, szIlosc, 31);
	
	iTarget = find_player("bhl", szNick);
	
	if(!iTarget) {
		client_print(id, print_console, "Nie znaleziono gracza!");
		return PLUGIN_HANDLED;
	}
	
	get_user_name(iTarget, szNick, 31);
	iSkrzynka = csgo_get_crate_by_name(szNazwaSkrzynki);
	
	if(!iSkrzynka) {
		client_print(id, print_console, "Nie znaleziono takiej skrzynki!");
		return PLUGIN_HANDLED;
	}
	
	iIlosc = str_to_num(szIlosc);
	
	csgo_set_user_crates(iTarget, iSkrzynka, csgo_get_user_crates(iTarget, iSkrzynka) + iIlosc);
	client_print(id, print_console, "Graczowi %s dodano %dx %s", szNick, iIlosc, szNazwaSkrzynki);
	return PLUGIN_HANDLED;
}

public cmd_DodajKlucze(id, iLevel, iCId)
{
	if(!cmd_access(id, iLevel, iCId, 3))
		return PLUGIN_HANDLED;
	
	new szNick[32], szIlosc[32], iIlosc, iTarget;
	
	read_argv(1, szNick, 31);
	read_argv(2, szIlosc, 31);
	
	iTarget = find_player("bhl", szNick);
	
	if(!iTarget) {
		client_print(id, print_console, "Nie znaleziono gracza!");
		return PLUGIN_HANDLED;
	}
	
	get_user_name(iTarget, szNick, 31);
	iIlosc = str_to_num(szIlosc);
	
	csgo_set_user_keys(iTarget, csgo_get_user_keys(iTarget) + iIlosc);
	client_print(id, print_console, "Graczowi %s dodano %dx klucz do skrzynki", szNick, iIlosc);
	return PLUGIN_HANDLED;
}

public cmd_DodajSkiny(id, iLevel, iCId)
{
	if(!cmd_access(id, iLevel, iCId, 5))
		return PLUGIN_HANDLED;
	
	new szNick[32], szIdBroni[4], szNazwaSkina[32], szIlosc[32], iIlosc, iTarget, iIdBroni, iSkin;
	
	read_argv(1, szNick, 31);
	read_argv(2, szIdBroni, 31);
	read_argv(3, szNazwaSkina, 31);
	read_argv(3, szIlosc, 31);
	
	iTarget = find_player("bhl", szNick);
	
	if(!iTarget) {
		client_print(id, print_console, "Nie znaleziono gracza!");
		return PLUGIN_HANDLED;
	}
	
	iIdBroni = str_to_num(szIdBroni);
	
	if(!(1 <= iIdBroni <= CSW_P90)) {
		client_print(id, print_console, "Nieprawidlowy index broni!");
		return PLUGIN_HANDLED;
	}
	
	iSkin = csgo_get_skin_by_name(iIdBroni, szNazwaSkina);
	
	if(!iSkin) {
		client_print(id, print_console, "Nie znaleziono skina id broni!");
		return PLUGIN_HANDLED;
	}
	
	new szNazwaBroni[32]; csgo_get_short_weaponname(iIdBroni, szNazwaBroni, 31);
	get_user_name(iTarget, szNick, 31);
	iIlosc = str_to_num(szIlosc);
	
	csgo_set_user_skins(iTarget, iIdBroni, iSkin, csgo_get_user_skins(iTarget, iIdBroni, iSkin) + iIlosc);
	client_print(id, print_console, "Graczowi %s dodano %dx Skin %s - %s", szNick, iIlosc, szNazwaBroni, szNazwaSkina);
	return PLUGIN_HANDLED;
}

public cmd_DodajEuro(id, iLevel, iCId)
{
	if(!cmd_access(id, iLevel, iCId, 3))
		return PLUGIN_HANDLED;
	
	new szNick[32], szIlosc[32], iIlosc, iTarget;
	
	read_argv(1, szNick, 31);
	read_argv(2, szIlosc, 31);
	replace(szIlosc, 31, ".", "");
	
	iTarget = find_player("bhl", szNick);
	
	if(!iTarget) {
		client_print(id, print_console, "Nie znaleziono gracza!");
		return PLUGIN_HANDLED;
	}
	
	get_user_name(iTarget, szNick, 31);
	iIlosc = str_to_num(szIlosc);
	
	csgo_set_user_keys(iTarget, csgo_get_user_keys(iTarget) + iIlosc);
	csgo_format_euro(iIlosc, szIlosc, 31);
	client_print(id, print_console, "Graczowi %s dodano %s Euro", szNick, szIlosc);
	return PLUGIN_HANDLED;
}